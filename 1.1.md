<center><h1> The Element of Programming </h1></center>

<center><h3>Jaewon Choi</h3></center>

---

# Computational process?

* 컴퓨터의 안에 있는 것으로, Data를 조작하면서 어떠한 일을 하는 것
* 이러한 규칙을 가리켜 **Program** 이라고 함
* 앞으로 우리는 이러한 Program 을 만들기 위한 여러가지 공부를 할 예정

---

# Programming languages?

* Program은 expression의 집합으로 이루어짐
* 이때, 이러한 expression을 쓰기 위해 쓰는 "말"을 **programming languages**라고 함
* 프로그래밍을 배우기 위해선 당연하게 배워야 하는 것 중 하나.

---

# Lisp

* 이 책은 Lisp, 정확히는 Lisp의 사투리인 scheme을 이용해 진행
* Symbolic expression 을 통해 계산을 했기 때문에, 최초에는 타 언어에 비해 느렸음
	* 1 / 3을 어떻게 evaluation 할 것인가?
* 지금은 Lisp compiler가 나왔기 때문에 충분히 빠르다고 함. (과연?)

---

# Why Lisp?

* data와 procedure를 엄격하게 구분 짓지 않는 편의성
	* C 언어를 떠올려보자... What the helll...
* 재밌다. 그렇단다.

---

# Actually, why should we learn SICP?

* Why did you join this study?

---

# Contents

---

<center><h1>The Element of Programming</h1></center>

---

# The Element of Programming

* 지시하는 수단 만이 좋은 언어의 조건이 아님. 이건 기본.
* 단순한 idea들을 모아 복잡한 idea를 만들어 낼 수 있는 수단에 집중해야 함.

---

# 좋은 프로그래밍 언어들이 갖추고 있는 것

* Primitive expression : 언어에서 가장 단순한 features이 존재하는가?
* Means of combination : PE를 엮을 수 있는가?
* Means of abstraction : combination에 이름을 붙여서 하나로 다룰 수 있는가?

---

# 프로그램을 짤 때 우리가 쓰는 두 가지

* Data : Procedure에서 쓰려는 물건
* Procedure : 데이터를 처리하는 규칙을 적은 것


---

<center><h1>Expression</h1></center>

---

# Scheme interpreter를 갖고 놀아보자!

*  expression을 넣으면
*  evalution 해서 값을 찍는다!
*  가장 간단 expression : 수를 나타내는 식
```scheme
2017
```
<pre>
<i>2017</i>
</pre>

---

# 좀 더 복잡한 식!

* 수를 나타낸 식과 primitive procedure를 엮어 compound expression을 나타낼 수 있다!
```scheme
(+ 137 349)
```
<pre>
<i>486</i>
</pre>
```scheme
(- 2.7 349)
```
<pre>
<i>-7.3</i>
</pre>

---

# 좀 더 복잡한 식!

* 여러 식을 괄호로 묶어 리스트로 만들고, Procedure application을 하도록 만든 compound expression을 **combination**이라고 부른다.
* 리스트 가장 왼쪽의 식을 operator, 나머지 식은 operand라고 부른다.
* operator가 나타내는 procedure에 operand를 argument로 넘기면 compound expression의 결과 값을 얻을 수 있다.

```scheme
(+ 137 349)
```
<pre>
<i>486</i>
</pre>

---

# Prefix notation

* operator를 왼쪽에 쓰는 방식
	* 수학에서 쓰는 1 + 2 같은 방식을 infix notation이라고 한다.
* prefix notation을 써서 얻는 장점 - 인자가 많아져도 됨
```scheme
(+ 21 35 12 7)
(* 25 4 12)
```

---

# Prefix notation

* prefix notation을 써서 얻는 장점 - Composition이 쉬움
```scheme
(+ (* 3 5) (- 10 6))
```

---

# Prefix notation


* 대신 더러워지고 읽기 어려워지기 쉽다
```scheme
(+ (* 3 (+ (* 2 4) (+ 3 5))) (+ (- 10 7) 6))
```

* 닝겐들을 위해 아래와 같이  pretty-printing 하자 (Is it pretty?)
```scheme
(+	(*	3
		(+ (* 2 4)
        	(+ 3 5)))
	(+	(-	10 7)
		6))
```

---

<center><h1>Name and Environment</h1></center>

---

# Name and Environment

* 가장 중요한 기능 중 하나 - Computational object에 이름을 붙히는 수단
* int i = 0
	* i : 변수
	* 0 (정확히는 i 라는 expression의 evaluation) : computational object

---

# 변수 갖고 놀기 (1)

* Scheme에서는 아래와 같은 방법으로 변수를 선언함
```scheme
(define size 2)
```

---

# 변수 갖고 놀기 (2)

* 연산은 아래와 같음
```scheme
(* 5 size)
```

---

# 이것저것 해보기

* Lesson: define 과 같은 keyword를 통해 compound operation을 간추릴 수 있다.

```scheme
(define pi 3.14159)

(define radius 10)

(* pi (* radius radius))

(define circumstance (* 2 pi radius))

circumstance
```

---

# Environment

* Environment : interpreter 안에 name-object pair를 저장해 놓은 메모리
* Global environment : 맨 바깥의 environment

---

<center><h1>How to evalute combination?</h1></center>

---

# How to evaluate combination?

* Example : 실행기가 combination의 값을 evalute 하는 차례가 다음과 같다고 하자.
	1. compound expression에서 subexpression의 값을 모두 구한다.
	2. compound expression에서 맨 왼쪽에 있는 operator의 값(주: 함수의 내용 역시 값으로 본다)은 프로시저가 되고, operand의 값은 argument가 된다. Procedure를 argument에 적용하여 compound expression의 값을 구한다.
* 얼핏 당연해 보이지만, 당연한가?

---

# How to evaluate combination?

* 규칙의 첫 단계 : recursion 맛
* Recursive process : 어떤 규칙의 한 단계에서 똑같은 규칙을 다시 밟도록 하는 process .

---

# How to evalute below combination?

```scheme
(*  (+ 2 (* 4 6))
    (+ 3 5 7))
```

=> Recursion!

* operator는 node로, 연산할 것들(즉, expression)은 branch로
* terminal node는 연산자 or 수가 달려 있음
* Recursion은 나무 꼴을 다루기에 알맞은 방법
* '값을 위로 올리는 ' 규칙 => tree accumulation

---

# How to evalute below combination?

<center><img src="https://github.com/jafffy/SICP/raw/master/ch1-Z-G-1.gif" width=500 style="margin-top: 40px" /></center>

---

# How to handle primitive expression?

* 숫자 식의 값은 여러 숫자가 모여서 나타내는 값이다.
	* 256은 숫자 식이고, 식의 값은 256이다.
* build-in operator의 값은 그 operator가 뜻하는 연산을 하도록 미리 묶어 놓은 machine instructions
* 그 밖의 다른 이름 값은 environment에서 그 이름으로 정의해 놓은 object.
	* 즉, 식 속에서 쓰는 이름이 무슨 뜻인지 결정짓는 것이 environment

---

# Specialty of primitive expression

* combination을 evaluation 하는 scheme은 (define x 3)을 설명하지 못한다.
	* define 이라는 procedure가 있는게 아니다. 즉, (define x 3)은 not compound expression. It is primitive!
* Special form: 보통의 계산 규칙으로는 값을 구하지 못하기 때문에 계산 규칙이 밝혀져 있어야 하는 문법. 즉, "대전제".
* Special form이 모이면 grammar가 된다.
* Syntactic sugar : 발암물질(?) 문법은 일관성을 깨먹지 않을 정도로 간명해야 한다.

---

<center><h1>Compound procedure</h1></center>

---

# Review on previous thing

* 수 & 산술 연산이 기본 데이터이자 기본 프로시져다.
* compound expression을 겹쳐 쓰는 것이 여러 연산을 한데 묶는 수단이 된다.
* name-value pair를 정의하는 것이 그나마 abstraction 하는 수단이 된다.

---

# How to define procedure?

* compound operation에 이름을 붙이자!
```scheme
(define (square x) (* x x))
```
* compound procedure 완성! 참 쉽죠?

---

# Procedures

```scheme
(define (<name> <formal parameter>) <body>)
```
* `<name>` : Environment에서 Procedure를 가리키는 이름
* `<formal parameter>` : local name. body에서만 쓰는 이름. (c.f. variable life cycle)
* `<body` : procedure를 부를 때마다 evaluate 하는 expression. argument로 `<formal parameter`를 replace 한 후, evaluate 한다.

---

# Examples on procedures

```scheme
(square 21)

(square (+ 2 5))

(square (square 3))

(+ (square x) (square y))

(define (sum-of-squares x y)
  (+ (square x) (square y))
  
(sum-of-squares 3 4)

(define (f a)
  (sum-of-squares (+ a 1 ) (* a 2)))
```
---

<center><h1>Substitution model</h1></center>

---

# How to evaluate procedure?

* compound procedure에 argument를 적용한다는 것은, procedure body의 모든 formal parameter들을 그에 대응하는 argument 값으로 맞바꾼 다음, evaluation 해서 얻어낸 식의 값을 구하는 것이다.

* What? (;;)

--- 

# How to evaluate `(f 5)`?

```scheme
(f 5)

(sum-of-squares (+ a 1) (* a 2))

(sum-of-squares (+ 5 1) (* 5 2))

(+ (square 6) (square 10))

(+ (* 6 6) (* 10 10))

(+ 36 100)

136
```
* 이러한 방법을 "back substitution" 이라고 한다.

---

# **주의 사항*

* 실제로 글자를 substitute 하는 것이 아님;;
* local environment에 밀어 넣고 계산하겠죠.
	* 4장에서 자세히 봅시다
* subsitution도 model of computation의 하나.
	* Mutable data가 나타난다면?
	* Mu.ta.ble.!!

---

# Applicative order vs normal order

* Lazy evaluation: 꼭 저렇게 모든 숫자를 미리부터 구해 놓아야 하는 것일까?
* 인자 이름으로 일단은 놓고, 나중에 모든게 primitive operation이 될 때 계산하면 되지!

---

# Normal-order evalutaion

```scheme
(f 5)

(sum-of-squares (+ 5 1) (* 5 2))

(+  (square (+ 5 1)) (square (* 5 2)) )

(+  (* (+ 5 1) (+ 5 1)) (* (* 5 2) (* 5 2)))

(+  (* 6 6)  (* 10 10))

(+ 36 100)

135
```

---

# 눈치 채셨나요?

* `(+ 5 1)`, `(* 5 2)`이 두 번씩 계산됨!
	* Dynamic programming이 출동하면 어떨까? => 예측이 가능할까? => caching 하면 되지! => cache size에 따라 어쩌구 저쩌구 (내적 갈등)
* Normal-order evaluation: 끝까지 펼친 다음에 줄이는 계산 방법
* Applicative-order evaluation: 인자 값부터 다 구하고 구하는 계산 법
* Lisp => Applicative-order evaluation! Why?
	* 두번 계산 안하기 때문에 빠르고
	* AOE가 통하지 않은 경우, NOE가 더 복잡하다.
		* 그른가...

---

<center><h1>Substitution model</h1></center>

---

# Conditional expressions and predicate

* How to evaluate this?

<center><img src="https://github.com/jafffy/SICP/raw/master/ch1-Z-G-2.gif" width=400 style="margin-top: 40px" /></center>

* we call it "Case analysis".

---

# cond - case analysis primitive expression

* conditional에서 따온 cond

```scheme
(define (abs x)
  (cond ((> x 0) x)
        ((= x 0) 0)
        ((< x 0) (- x))))
```

`((> x 0) x`에서 `(> x 0)`을 predicate이라고 하며, 이것은 True or False의 값을 가진다.

---

# cond - How to evaluate?

```scheme
(cond (<p1> <e1>) ;;; 먼저 <p1>을 구하고 참인지 본다. 참이면 e1
      (<p2> <e2>) ;;; <p1>이 거짓이면 <p2>를 evaluate.
      ...
      (<pn> <en>)) ;;; 끝까지 갔는데 시망이면? undefined.
```

---

# Primitive predicate - >, <, =
* 왠지 귀엽다. 아니 이게 아니고.
* 이 predicate들은 두 수를 인자로 받아서 대소-상등 비교를 한 뒤 True or False로 답을 내놓는 기본 프로시저.

---

# Other form of cond clause

```scheme
(define (abs x)
  (cond ((< x 0) (- x))
         (else x)))
```
* else: 모든 predicate들이 False 일 경우 evaluation 되는 predicate.

```scheme
; (if <predicate> <consequent> <alternative>)
; predicate이 참이라면 consequent or 거짓이면 alternative
(define (abs x)
  (if  (< x 0)
       (- x)
       x))
```

---

# Logical operations

* `(and <e1> ... <en>)` : 우리가 아는 and. e1이 거짓이면 그 뒤는 evaluate 하지 않는다.
* `(or <e1> ... <en>)` : 우리가 아는 or. e1이 참이면 그 뒤는 evaluate 하지 않는다.
* `(not <e1>)` : 우리가 아는 not.
* and랑 or는 모든 인자가 되는 식의 값을 구하지 않는다는 것을 알고 써야 한다. not은 보통 procedure다.

---

# Logical operations - examples

```scheme
(and (> x 5) (< x 10))

(define (>= x y)
  (or (> x y) (= x y))
  
(define (>= x y)
  (not (< x y))
```

---

<center><h1>Practices:<br>Square root by Newton method</h1></center>

---

# Math functions vs procedures

* 수학에서의 함수는 선언적. 조건의 경제성이 가장 중요하다.
* 전산에서의 procedure는 구체적. 효율성이 가장 중요하다.
* Question: y >= 0 이고 y^2 = x 일 때 sqrt(x) 는 y 다.
=> ok, math. What about computation?
```scheme
(define (sqrt x)
  (the y (and (>= y 0)
              (= (square y) x)))) ; ???
```

---

# Getting Square root by Newton's method

<center><img src="https://wikimedia.org/api/rest_v1/media/math/render/svg/710c11b9ec4568d1cfff49b7c7d41e0a7829a736" width=400 style="margin-top: 40px" /></center>	

* ...은 일반식이고 여기선 추상화된 방법을 쓴다.
* x의 제곱근을 구한다. x의 제곱근과 가까운 값 y를 가정하자.
	* y와 x / y의 평균을 구한다.
	* 이 결과를 다음 y로 잡는다.
	* y가 괜찮을 때까지 반복한다.

---

# 음 일단 scheme으로 짜면...

```scheme
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))
```

* taily recursion 을 하도록 작성해야 compile optimization을 적용받을 수 있음.

---

# improve, average의 구현

```scheme
(define (improve guess x)
  (average guess (/ x guess)))
  
(define (average x y)
  (/ (+ x y) 2))
```

---

# good-enough? 의 구현

```scheme
(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))
```

* 더 좋게 하려면 어떻게 해야할까?

---

# Bootstrapper

```scheme
(define (sqrt x)
  (sqrt-iter 1.0 x))
  
(sqrt 9) ; 3.00009155413138

(sqrt (+ 100 37)) ; 11.704699917758145

(square (sqrt 1000)) ; 1000.000369924366 => 1000이 아님
```

* 참 쉽죠?

---

<center><h1>Procedures as Black-Box Abstractions</h1></center>

---

# Abstraction

* procedure를 묶는 기준
	* 멍청하게 묶기: 10줄씩 묶는다 => Does it make sense?
	* 우리가 해야 하는 것: Module화 하여 재사용성을 보장해야 한다.
* Procedure abstraction
	* 내부가 어떻게 돌아가는지 몰라도 가져다 쓸 수 있게 간추리는 방법

* scope
	* 어떠한 이름의 유효 범위. procedure 내의 local variable은 그 procedure 내에서만 의미를 갖는다.

---

# Bound variable & free variable

* bound variable: procedure에 묶여 있으며, 이들의 이름을 모두 똑같이 바꾸기만 하면 프로시저의 의미는 달라지지 않음.
	* arguments나 local variable이 이렇겠다.
* free variable: procedure의 정의에 묶여 있지 않는다.
	* 가져다 쓰는 procedures, global variables...
```scheme
(define (improve guess x) ; guess, x 는 bound.
  (average guess (/ x guess))) ; /, average는 free.
  
(define (average x y)
  (/ (+ x y) 2))
```

---

# Internal structure

```scheme
(define (sqrt x)
  (sqrt-iter 1.0 x))
  
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))
                 (define (improve guess x)
(average guess (/ x guess)))
  
(define (average x y)
  (/ (+ x y) 2))
  
(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))
```
* 이게 sqrt를 갖다 쓰는 사람들이 모두 봐야 하는 코드일까?

---

# Internal structure & block structure

* 사람들이 알기 원하는 것은 `(define (sqrt x)...` 코드 뿐이다.
* 다른 사람들이 average 라는 이름을 쓰고 싶을 때 쓸 수 없다.
* 개선이 필요하다!

---

# Block structure

```scheme
(define (sqrt x)
  (define (good-enough? guess x)
    (< (abs (- (square guess) x)) 0.001))
  (define (improve guess x)
    (average guess (/ x guess)))
  (define (sqrt-iter guess x)
    (if (good-enough? guess x)
        guess
        (sqrt-iter (improve guess x) x)))
  (sqrt-iter 1.0 x))
```

---

# Block structure with lexical scoping

```scheme
(define (sqrt x)
  (define (good-enough? guess)
    (< (abs (- (square guess) x)) 0.001))
  (define (improve guess)
    (average guess (/ x guess)))
  (define (sqrt-iter guess)
    (if (good-enough? guess)
        guess
        (sqrt-iter (improve guess))))
  (sqrt-iter 1.0))
```

* block 안의 inner-procedure 입장에서 x가 free variable로 보인다
	* bound variable이 많을 수록 관리가 귀찮다는 면에서 개선.
* 코드가 짧아진다. 개이득.
* 단, 과도한 추상이 되지 않도록 주의. 재사용성을 해치는 면도 있다.